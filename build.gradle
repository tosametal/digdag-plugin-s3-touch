plugins {
    id 'scala'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id "com.diffplug.gradle.spotless" version "3.16.0"
}


group =  'io.digdag.plugin'
version = '0.0.2'

def digdagVersion = "0.9.31"
def scalaSemanticVersion = "2.12.7"
def s3SdkVersion = "1.11.467"
def guavaVersion = "19.0"
def specs2Version = "4.3.4"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/digdag/maven'
    }
}

dependencies {
    compile group: 'io.digdag', name: 'digdag-spi', version: digdagVersion
    compile group: 'io.digdag', name: 'digdag-plugin-utils', version: digdagVersion
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaSemanticVersion
    compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: s3SdkVersion
    testCompile group: 'org.specs2', name: 'specs2-core_2.12', version: specs2Version
    testCompile group: 'org.specs2', name: 'specs2-junit_2.12', version: specs2Version
}

sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala']
        }
    }
}

shadowJar {
    classifier = null
    dependencies {
        exclude(dependency('io.digdag:.*'))
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

spotless {
    scala {
        scalafmt('1.5.1').configFile('.scalafmt.conf')
    }
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

compileScala.options.encoding = 'UTF-8'
compileTestScala.options.encoding = 'UTF-8'
compileScala.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"